apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'eclipse-wtp'

version = 1.0

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
	mavenCentral()
}

ext {
	springVersion = '3.2.4.RELEASE'
	tilesVersion = '2.2.2'

	slf4jVersion = '1.7.5'
	logbackVersion = '1.0.13'

	servletVersion = '3.0.1'
	jspVersion = '2.2.1'
	jstlVersion = '1.2'

	junitVersion = '4.11'
	hamcrestVersion = '1.3'
	mockitoVersion = '1.9.5'
}

dependencies {
	compile ("org.springframework:spring-webmvc:$springVersion") {
		// Spring uses commons-logging, but we want to use logback instead
		exclude(group: 'commons-logging', module: 'commons-logging')
	}
	compile 'org.apache.commons:commons-lang3:3.0'

	providedCompile "javax.servlet:javax.servlet-api:$servletVersion"
	providedCompile "javax.servlet.jsp:javax.servlet.jsp-api:$jspVersion"

	testCompile "junit:junit:$junitVersion"
	testCompile "org.hamcrest:hamcrest-core:$hamcrestVersion"
	testCompile "org.mockito:mockito-core:$mockitoVersion"
	testCompile "org.springframework:spring-test:$springVersion"
	
	runtime "jstl:jstl:$jstlVersion"

	runtime "org.apache.tiles:tiles-api:$tilesVersion"
	runtime "org.apache.tiles:tiles-core:$tilesVersion"
	runtime "org.apache.tiles:tiles-jsp:$tilesVersion"
	runtime "org.apache.tiles:tiles-servlet:$tilesVersion"

	runtime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
	runtime "ch.qos.logback:logback-classic:$logbackVersion"
}

test {
	testLogging.showStandardStreams = true
}

eclipse {
	project.natures 'org.springframework.ide.eclipse.core.springnature'

	// Hide Gradle's 'build' folder from Eclipse
	project {
		file {
			withXml { xmlProvider ->
				Node project = xmlProvider.asNode()
				Node filter = project.appendNode('filteredResources').appendNode('filter')
				filter.appendNode('id', 1379968350622) 
				filter.appendNode('name', '')
				filter.appendNode('type', 26)
				Node matcher = filter.appendNode('matcher')
				matcher.appendNode('id', 'org.eclipse.ui.ide.multiFilter')
				matcher.appendNode('arguments', '1.0-projectRelativePath-matches-false-false-build')
			}
		}
	}

	wtp {
		facet {
			facet name: 'jst.web', version: '3.0'
			facet name: 'jst.java', version: '1.6'
		}
	}

	classpath.downloadSources = true
    classpath.downloadJavadoc = true
}

// Change context path (base url) otherwise defaults to name of project
jettyRun.contextPath = ''
jettyRunWar.contextPath = ''
